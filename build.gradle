plugins {
    id "cosmicloom"
    id "maven-publish"
}

loom {
    accessWidenerPath = file("src/main/resources/flux-api.accesswidener")
}

repositories {
    flatDir {
        dirs "lib"
    }
}

dependencies {
    cosmicReach(loom.getCosmicReach(cosmic_reach_version))
    modImplementation(loom.getCosmicQuilt(cosmic_quilt_version))
    runtimeOnly(":testmod:")
}

processResources {
    // Locations of where to inject the properties
    def resourceTargets = [
        "quilt.mod.json"
    ]

    // Left item is the name in the target, right is the variable name
    def replaceProperties = [
        "mod_version" : project.version,
        "mod_group" : project.group,
        "mod_name" : project.name,
        "mod_id" : id,
        "mod_desc" : flux_desc,
        "cosmic_reach_version": cosmic_reach_version,
    ]

    inputs.properties replaceProperties
    replaceProperties.put "project", project
    filesMatching(resourceTargets) {
        expand replaceProperties
    }
}

java {
    withSourcesJar()
    // withJavadocJar()

    // Sets the Java version
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
}


publishing {
    publications {
        maven(MavenPublication) {
            groupId = group
            artifactId = id

            from components.java
        }
    }
}
